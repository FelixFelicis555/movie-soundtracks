SELECT ?movie ?AnzSongs ?RockSongs ?LoveSongs
FROM <http://imn.htwk-leipzig.de/pbachman/ontologies/imdb#>
FROM <http://imn.htwk-leipzig.de/pbachman/ontologies/lastfm#>
FROM <http://imn.htwk-leipzig.de/pbachman/ontologies/tunefind#>
FROM <http://imn.htwk-leipzig.de/pbachman/ontologies/references#>
WHERE
{  
    {
        ?imdbmovie a dbpedia-owl:Film ;
                   dbpprop:title ?movie ;
                   dbpedia-owl:director ?dir ;
                   owl:sameAs ?tunefindFilm .

        ?dir dbpprop:name ?name .
	
        FILTER (?name = "Michael Bay") .
    }

    {
        SELECT (COUNT(DISTINCT ?song) as ?AnzSongs) ?tunefindFilm
        WHERE
        {
            ?tunefindFilm tunefind:contains ?song .
        }
        GROUP BY ?tunefindFilm
    }

    {
        SELECT (COUNT(DISTINCT ?song) as ?RockSongs) ?tunefindFilm
        WHERE
        {
            ?tunefindFilm tunefind:contains ?song .
            ?song rdfs:label ?s ;
                  owl:sameAs ?lastFmSong .

            ?lastFmSong lastfm:tagged ?tag .

            FILTER (CONTAINS(lcase(?tag), "rock"))
        }
        GROUP BY ?tunefindFilm
    }

    {
        SELECT (COUNT(DISTINCT ?song) as ?LoveSongs) ?tunefindFilm
        WHERE
        {
            ?tunefindFilm tunefind:contains ?song .
            ?song rdfs:label ?s ;
                  owl:sameAs ?lastFmSong .

            ?lastFmSong lastfm:tagged ?tag .

            FILTER (CONTAINS(lcase(?tag), "love"))
        }
        GROUP BY ?tunefindFilm
    }
}