PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX dbpedia-owl: <http://dbpedia.org/ontology/>
PREFIX dbpprop: <http://dbpedia.org/property/>

CONSTRUCT
{
    ?tunefindSong owl:sameAs ?lastfmSong .
    ?lastfmSong owl:sameAs ?tunefindSong .

    ?tunefindArtist owl:sameAs ?lastFmArtist .
    ?lastFmArtist owl:sameAs ?tunefindArtist .
}
WHERE
{
    ?tunefindSong rdf:type dbpedia-owl:Song ;
                  rdfs:label ?tuneFindSongLabel ;
                  dbpedia-owl:artist ?tunefindArtist .

    FILTER(STRSTARTS(STR(?tunefindSong), "http://imn.htwk-leipzig.de/pbachman/ontologies/tunefind#"))

    ?lastfmSong rdf:type dbpedia-owl:Song ;
                rdfs:label ?lastFmSongLabel ; 
                dbpedia-owl:artist ?lastFmArtist .

    FILTER(STRSTARTS(STR(?lastfmSong), "http://imn.htwk-leipzig.de/pbachman/ontologies/lastfm#"))


    FILTER(lcase(STR(?tuneFindSongLabel)) = lcase(STR(?lastFmSongLabel)))
}