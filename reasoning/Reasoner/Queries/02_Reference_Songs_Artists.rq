PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX dbpedia-owl: <http://dbpedia.org/ontology/>
PREFIX dbpprop: <http://dbpedia.org/property/>

INSERT
{
    GRAPH <http://imn.htwk-leipzig.de/pbachman/ontologies/references#>
    {
        ?tunefindSong owl:sameAs ?lastfmSong .
        ?lastfmSong owl:sameAs ?tunefindSong .

        ?tunefindArtist owl:sameAs ?lastFmArtist .
        ?lastFmArtist owl:sameAs ?tunefindArtist .
    }
}
USING <http://imn.htwk-leipzig.de/pbachman/ontologies/tunefind#>
USING <http://imn.htwk-leipzig.de/pbachman/ontologies/lastfm#>
WHERE
{
    ?tunefindSong a dbpedia-owl:Song ;
                  dbpedia-owl:artist ?tunefindArtist ;
                  dbpprop:title ?tunefindSongTitle .

    ?tunefindArtist a dbpedia-owl:MusicalArtist ;
                    dbpprop:name ?tunefindArtistName .

    FILTER(STRSTARTS(STR(?tunefindSong), "http://imn.htwk-leipzig.de/pbachman/ontologies/tunefind#"))

    ?lastfmSong a dbpedia-owl:Song ;
                dbpedia-owl:artist ?lastfmArtist ;
                dbpprop:title ?lastfmSongTitle .

    ?lastfmArtist a dbpedia-owl:MusicalArtist ;
                  dbpprop:name ?lastfmArtistName .

    FILTER(STRSTARTS(STR(?lastfmSong), "http://imn.htwk-leipzig.de/pbachman/ontologies/lastfm#"))


    FILTER(lcase(STR(?tunefindSongTitle)) = lcase(STR(?lastfmSongTitle)))
    FILTER(lcase(STR(?tunefindArtistName)) = lcase(STR(?lastfmArtistName)))
}